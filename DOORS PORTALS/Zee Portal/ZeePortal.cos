* Script 1 extracted from OfflineWarpPortals.agents by Mirality REVELATION

inst
*How many portals are already in the world?
doif totl 3 9 38001 = 0
*If there aren't any, create two portals
	setv va00 2
else
*Otherwise, create 1 portal
	setv va00 1
endi
reps va00
*Offline portals have the exactly same attributes as their online counterparts
	new: simp 3 9 38001 "mediporter" 9 0 400
	attr 199
	clac 2
	elas 0
	accg 5
	aero 10
	perm 60
*	puhl -1 95 110
*ov00 = is it open or closed?
	setv ov00 0
*ov01 = is it functioning (Is blue swirl present?)
	setv ov01 0
*ov02 = portal to warp to
	seta ov02 null
*ov03 = was portal just used?
	setv ov03 0
*Dump the portals into the inventory
	seta va01 targ
	enum 1 2 11
		spas targ va01
		doif ov00 = 0 and clac = 0
			mesg writ targ 0
		endi
	next
repe

*Deactivate - or rather, open/close the portal
scrp 3 9 38001 0
*Don't do this if being carried
	doif carr = null
*Remove interruptions
		lock
*If you're closed....
		doif ov00 = 0
*Lock it down in place
			attr 197
*Animate, make a noise, and open up
			anim [0 1 2 3 4 5]
			sndc "por1"
			over
*Start timer, let creatures push you, and mark yourself as "open"
			tick 1
			bhvr 1
			setv ov00 1
*Otherwise, do the inverse
		else
*If you've got a blue swirl, turn that off first
			doif ov01 = 1
*Fade out
				anim [8 8 7 6 5]
				setv ov01 0
				fade
				over
			endi
			sndc "por2"
*Turn off tick, don't let creatures push you, and mark yourself as closed
			tick 0
			bhvr 0
			setv ov00 0
			anim [4 3 2 1 0]
			over
*Become carriable again
			attr 199
		endi
	endi
endm

*Portal push
scrp 3 9 38001 1
*Remove interruptions
	lock
*Did a creature activate this?
	doif crea from = 1
*Before anything else, make sure you're open
		doif ov01 = 1
*Can you be used?
			doif ov03 = 0
*Make a noise
				sndc "wr_e"
*Tell creature to wait
				stim writ from 75 1
*Get its identification
				seta va99 from
*Loop until you find another open portal that isn't you
				loop
					inst
					rtar 3 9 38001
					doif targ <> null
						doif targ <> ownr
							doif ov00 <> 0 and ov01 <> 0
								seta mv02 targ
							endi
						endi
					endi
					targ ownr
					slow
				untl ov02 <> null
*Get fellow portal's position
				targ ov02
				setv va00 posx
				setv va01 posy
				addv va01 50
				targ va99
*Move it, move it!
				doif targ <> null
*Is it moving on its own?
					doif movs <> 0
*If not, check to see if it's in a vehicle
						doif movs = 3
*Target the vehicle and make it drop the creature
							targ carr
							dpas 4 0 0
							targ va99
						else
*Don't move it!
							stop
						endi
					endi
					mvsf va00 va01
*Target the other portal, make it make a noise, and make it so it can't be used again for a short while
					targ mv02
					setv ov03 1
					sndc "wr_a"

*Simulate actual warp jump
					targ va99
					aslp 0
					pose 59
*Tell creature it warped and used a metaroom door
					stim writ targ 27 1
					stim writ targ 95 1
					wait 30
				endi
				targ ownr
			else
*If it's not open, tell the creature that it's wasting its time
				stim writ from 0 1
			endi
		endi
	endi
endm

*Portal timer
scrp 3 9 38001 9
*Check for other open portals in the world
	setv va00 0
	enum 3 9 38001
		doif targ <> null
			doif targ <> ownr
				doif ov00 <> 0
					addv va00 1
				endi
			endi
		endi
	next
*If there is another open portal in the world and you're off, turn on
	doif va00 >= 1 and ov01 = 0
*Start humming
		sndl "humm"
		anim [6 7 8]
		over
*Mark yourself as open
		setv ov01 1
		anim [8 255]
*If you're the only open portal in the world, turn off
	elif va00 = 0 and ov01 = 1
*Fade out
		anim [8 8 7 6 5]
		setv ov01 0
		fade
		over
	endi
*If you've just been used, wait a bit before turning back on
	doif ov03 = 1
		wait 30
		setv ov03 0
	endi
endm

*Portal exception - this is a failsafe
scrp 3 9 38001 255
*Unfortunately, there's not a lot to put here
endm

*Removal
rscr
enum 3 9 38001
	kill targ
next
scrx 3 9 38001 0
scrx 3 9 38001 1
scrx 3 9 38001 9
scrx 3 9 38001 255