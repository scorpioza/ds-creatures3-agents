*Fishnut only
**Sprites and scripting by tomtschek

**produce a fishseedproducing dummy but kill if already there
enum 1 1 31936
	kill targ
next

setv va00 game "ng4x"
setv va01 game "ng4y"
addv va00 1767
addv va01 103


new: simp 1 1 31936 "hfischnut" 1 0 1000
bhvr 0
attr 0
perm 60
tick 100
mvsf va00 va01


*************
*fishnut
**************

*tickscript to produce seed

scrp 1 1 31936 9

*Look and see if there are other nuts  in the game
	doif totl 2 3 31536 < 30
		setv va20 rand 1 20

		doif game "ng1" = 1 and va20 < 9
			setv va04 rand 0 1
			setv va00 game "ngx"
			setv va01 game "ngy"
			doif va04 = 0
				addv va00 rand 38 228
				addv va01 rand 148 206
			elif va04 = 1
				addv va00 rand 245 420
				addv va01 rand 288 421
			endi
		elif game "ng4" = 1 and va20 >= 9
* set a random position for the nut to grow

			setv va00 game "ng4x"
			setv va01 game "ng4y"
			addv va00 rand 1700 2500
			addv va01 rand 103 328
		endi

* produce a nut 

		new: simp 2 3 31536 "hfischnut" 1 0 3000
		attr 195
		bhvr 48
		elas 30
		fric 50
		accg 10
		aero 5
		mvsf va00 va01
		tick rand 1 2
		setv ov01 0
		setv ov99 0
		emit 7 .3

	endi
endm




* tick script for the seed
scrp 2 3 31536 9
	inst
	addv ov01 1



*if falling into water-drif to surface 
	doif rtyp room targ = 8 or rtyp room targ = 9
		inst
		setv ov99 1
		accg 0
		aero 50
		velo 0 -1


*if coming out of water swim
	else
		doif ov99 = 1
			velo 0 0
			accg 0
			aero 5

		else
			accg 8
			aero 5
		endi
	endi


	doif ov01 > 500
		kill ownr
	endi
endm


*eat script
scrp 2 3 31536 12
	lock
	snde "chwp"
	stim writ from 77 3

* Glucose
	chem 3 0.5
* Glycogen	
	chem 4 0.5
* Starch	
	chem 5 0.5		
* Fatty acid	
	chem 6 0.3
* Triglyceride	
	chem 8 0.5
* Adipose tissue: When your creature needs to store fat for the long-term, the fat becomes this. This is the stuff that makes them fat, but they'll never really be obese.	
	chem 9 0.5	
* Fat	
	chem 10 0.3	
* Protein	
	chem 12 1.0
* Amino acid	
	chem 13 0.3		
* Anti-oxidant:	Cures carbon monoxide poisoning.	
	chem 93 0.5
* Sodium thiosulphate: Cures cyanide poisoning.	
	chem 96 0.5
* Arnica: Cures glycotoxin poisoning.	
	chem 97 0.5	
* Vitamin E: Keeps reproductive organs healthy.			
	chem 98 0.5
* Vitamin C: Keeps bones healthy, aids in healing from injuries.
	chem 99 0.5	
* Antihistamine	
	chem 100 0.5	
* Life: Creature ages as this depletes. A creature dies of old age when it nears zero. Continually injecting this will effectively make your creature immortal.	
	chem 125 0.5
	
	kill ownr
endm

*collision
scrp 2 3 31536 6
	snde "nfis"
endm

*drop
scrp 2 3 31536 5
	setv ov99 0


endm

scrx 1 1 31936 9
scrx 2 3 31536 6
scrx 2 3 31536 9
scrx 2 3 31536 12