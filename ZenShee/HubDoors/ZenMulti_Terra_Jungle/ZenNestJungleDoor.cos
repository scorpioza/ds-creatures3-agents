		doif game "CoC_UH" = 1
			enum 5 5 24401
				doif ov00 = 4
					setv va00 posl
					setv va01 post
					kill targ
				endi
			next
		else
			rtar 1 1 10405
			setv va00 posl
			setv va01 post
			kill targ			
			targ ownr
		endi


		new: comp 2 2 38021 "doorhub" 14 42 0
* mvto the location of the late dummy door
		addv va00 1
* 			mvto the location of the late dummy door
		mvto va00 va01
* Xekima: just in case if dummy door was removed in previous installations...
* 		mvto 42064 29604
* user can click on door and when s/he does run script 1
		attr 4
		clac 0
		bhvr 1
		setv va00 room targ
** set the entry
		setv ov00 1
		setv ov01 1
		setv ov02 1
		pat: butt 1 "doorhub" 56 4 147 22 0 [] 1000 0
		pat: butt 2 "doorhub" 57 3 147 45 0 [] 1001 0
		pat: butt 3 "doorhub" 58 2 147 70 0 [] 1002 0


		new: comp 2 2 38026 "door" 14 75 1
		mvto 7050 5290
* user can click on door and when s/he does run script 1
		attr 4
		clac 0
		bhvr 1
		tran 0 0
** link the rooms, spread the CA around
*		link va00 room targ 5
		setv ov00 1
		setv ov01 1
		setv ov02 1


* Script 2 extracted from CoC_CAT.agents by Mirality REVELATION
* Thank you Mirality! -Zzzzoot

* UH door activate
scrp 2 2 38021 1
	lock
* check for lock
	doif from <> pntr
		targ from
		setv va00 gnus
		targ ownr
		setv va01 0
*			if norn check if allowed
		doif va00 = 1 and avar ownr ov00 = 1
*				allow
			setv va01 1
*			if grendel check if allowed
		elif va00 = 2 and avar ownr ov01 = 1
*				allow
			setv va01 1
*			if ettin check if allowed
		elif va00 = 3 and avar ownr ov02 = 1
*			allow
			setv va01 1
		endi
	else
		setv va01 1
	endi
	doif va01 = 0
*		disappoint
		stim writ from 0 1
		stop
	else
*		wait
		stim writ from 75 1
	endi
** open
*	self
	anim [1 2 3 4 5 6 7 8 9 10 11 12 13]
*	partner
	rtar 2 2 38026
	anim [0 1 2 3 4 5 6 7 8 9 10 11 12 13]
*	get position of partner
	setv va02 posx
	setv va03 posy
	addv va03 40
	targ ownr
	over
** move
	doif from = pntr
*		move the camera, then all touching creatures
		cmrp va02 va03 0
		etch 4 0 0
			doif targ <> null
				doif dead = 0 and aslp = 0
					mvsf va02 va03
				endi
			endi
		next
	else
		targ from
		inst
		doif targ <> null
			doif aslp = 0 and dead = 0 and carr = null and aslp = 0
				mvsf va02 va03
				stim writ targ 95 1
			endi
		endi
		slow
		targ ownr
	endi
** close
*	self
	anim [12 11 10 9 8 7 6 5 4 3 2 1 0]
*	partner
	rtar 2 2 38026
	anim [13 12 11 10 9 8 7 6 5 4 3 2 1 0]
endm

* HydroBay door activate
scrp 2 2 38026 1
	lock
	inst
	doif from <> null
		stim writ from 75 1
	endi
	slow
** open
*	self
	anim [0 1 2 3 4 5 6 7 8 9 10 11 12 13]
*	partner
	rtar 2 2 38021
	anim [0 1 2 3 4 5 6 7 8 9 10 11 12]
	over
*	get position of partner
	setv va02 posx
	setv va03 posy
*addv va03 40
	targ ownr
** move
	doif from = pntr
*		move the camera, then all touching creatures
		cmrp va02 va03 0
		etch 4 0 0
			doif targ <> null
				doif dead = 0 and aslp = 0
					mvsf va02 va03
				endi
			endi
		next
	else
		targ from
		inst
		doif targ <> null
			doif aslp = 0 and dead = 0 and carr = null and aslp = 0
				mvsf va02 va03
				stim writ targ 95 1
			endi
		endi
		slow
	endi
	targ ownr
** close
*	self
	anim [13 12 11 10 9 8 7 6 5 4 3 2 1 0]
*	partner
	rtar 2 2 38021
	anim [12 11 10 9 8 7 6 5 4 3 2 1 0]
endm



** change the settings UH
* norn
scrp 2 2 38021 1000
	doif ov00 = 1
		part 1
		pose 3
		part 0
		setv ov00 0
	else
		part 1
		pose 0
		setv ov00 1
	endi
endm
* grendel
scrp 2 2 38021 1001
	doif ov01 = 1
		part 2
		pose 2
		setv ov01 0
	else
		part 2
		pose 0
		setv ov01 1
	endi
endm
* ettin
scrp 2 2 38021 1002
	doif ov02 = 1
		part 3
		pose 1
		setv ov02 0
	else
		part 3
		pose 0
		setv ov02 1
	endi
endm

*rscr
*enum 2 2 38026
*	kill targ
*next
*enum 2 2 38021
*	kill targ
*next
