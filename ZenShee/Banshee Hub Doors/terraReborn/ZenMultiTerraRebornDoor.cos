new: simp 1 3 888 "fav_place_terra" 1 0 1 attr 272 mvto 11800 125 cmrt 0 tick 10


		doif game "CoC_UH" = 1
			enum 5 5 24401
				doif ov00 = 6
					setv va00 posl
					setv va01 post
					kill targ
				endi
			next
		else
			rtar 1 1 10406
			setv va00 posl
			setv va01 post
			kill targ			
			targ ownr
		endi

		new: comp 2 2 60244 "tr_bottom_door_from_hub" 14 0 0
* mvto the location of the late dummy door
		addv va00 1
* 		mvto the location of the late dummy door
		mvto va00 va01		
*		mvto 40240 2382
* user can click on door and when s/he does run script 1
		attr 4
		clac 0
		bhvr 1
		setv va00 room targ
** set the entry
		setv ov00 1
		setv ov01 1
		setv ov02 1
		pat: butt 1 "tr_bottom_door_from_hub" 14 4 -35 22 0 [] 1000 0
		pat: butt 2 "tr_bottom_door_from_hub" 15 3 -35 45 0 [] 1001 0
		pat: butt 3 "tr_bottom_door_from_hub" 16 2 -35 70 0 [] 1002 0

* MOR
		new: comp 2 2 60243 "ZenDoor" 4 0 0
		mvto 10975 935
* user can click on door and when s/he does run script 1
		attr 4
		clac 0
		bhvr 1
		tran 0 0
** link the rooms, spread the CA around
		link va00 room targ 5
		setv ov00 1
		setv ov01 1
		setv ov02 1




* UH activate
scrp 2 2 60244 1
	lock
	inst
	doif from <> null
		stim writ from 75 1
	endi
	slow
** open
*	self
	anim [1 2 3 4 5 6 7 8 9 10 11 12 13]
*	partner
	rtar 2 2 60243
	anim [0 1 2 3]
*	get position of partner
	setv va02 posx
	setv va03 posy
	addv va03 40
	targ ownr
	over
** move
	doif from = pntr
		cmrp va02 va03 0
		etch 4 0 0
			doif targ <> null
				doif dead = 0 and aslp = 0
					mvsf va02 va03
				endi
			endi
		next
	else
		targ from
		inst
		doif targ <> null
			doif aslp = 0 and dead = 0 and carr = null and aslp = 0
				mvsf va02 va03
				stim writ targ 95 1
			endi
		endi
		targ ownr
	endi
** close
*	self
	anim [12 11 10 9 8 7 6 5 4 3 2 1 0]
*	partner
	rtar 2 2 60243
	anim [3 2 1 0]
endm

* DAE activate
scrp 2 2 60243 1
	lock
	inst
	doif from <> null
		stim writ from 75 1
	endi
	slow
** open
*	self
	anim [0 1 2 3]
*	partner
	rtar 2 2 60244
	anim [0 1 2 3 4 5 6 7 8 9 10 11 12]
	over
*	get position of partner
	setv va02 posx
	setv va03 posy
	addv va03 40
	targ ownr
** move
	doif from = pntr
		cmrp va02 va03 0
		etch 4 0 0
			doif targ <> null
				doif dead = 0 and aslp = 0
					mvsf va02 va03
				endi
			endi
		next
	else
		targ from
		inst
		doif targ <> null
			doif aslp = 0 and dead = 0 and carr = null and aslp = 0
				mvsf va02 va03
				stim writ targ 95 1
			endi
		endi
		slow
		targ ownr
	endi
** close
*	self
	anim [3 2 1 0]
*	partner
	rtar 2 2 60244
	anim [12 11 10 9 8 7 6 5 4 3 2 1 0]
endm


** change the settings UH
* norn
scrp 2 2 60244 1000
	doif ov00 = 1
		part 1
		pose 3
		setv ov00 0
	else
		part 1
		pose 0
		setv ov00 1
	endi
endm
* grendel
scrp 2 2 60244 1001
	doif ov01 = 1
		part 2
		pose 2
		setv ov01 0
	else
		part 2
		pose 0
		setv ov01 1
	endi
endm
* ettin
scrp 2 2 60244 1002
	doif ov02 = 1
		part 3
		pose 1
		setv ov02 0
	else
		part 3
		pose 0
		setv ov02 1
	endi
endm



*rscr
*enum 2 2 60244
*	kill targ
*next
*enum 2 2 60243
*	kill targ
*next
*setv va00 1000
*loop
*	scrx 2 2 60244 va00
*	scrx 2 2 60243 va00
*	addv va00 1
*untl va00 > 10000




