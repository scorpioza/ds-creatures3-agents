* C1 Morning Glory

* Dependencies:
*  c1plants.c16
*  chwp.wav

inst
lock

new: vhcl 2 4 32303 "c1plants" 1 20 100
attr 735
cabn 8 1 46 39
accg 25
fric 100
aero 10
elas 0
bhvr 0
perm 60
tick rand 900 1000
mesg wrt+ targ 1000 0 0 10

doif totl 2 4 32303 <= 1
	mvsf 9325 8812

elif type game "ds_game_type" ne 2
* 		it's NOT a string, this must be C3 only
* 		choose a safe C3 location
	setv va00 5440
	setv va01 3580
	velo rand 30 40 -5
	mvsf va00 va01
else
* 		it IS a string, but what is the value?
	doif game "ds_game_type" eq "undocked"
* 			this is an undocked world, location needs to be DS safe
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	elif game "ds_game_type" eq "docked"
* 			this is a docked world, location can be anywhere in the DS/C3 realm
		setv va00 6100
		setv va01 9200
		velo rand 30 40 -5
		mvsf va00 va01
	endi
endi

*************
* Scripts

scrp 2 4 32303 1
*	Morning Glory clicked
	inst
	doif game "C1_Clickable_Food" = 1
		mesg wrt+ ownr 1000 1 0 0
	endi
endm

scrp 2 4 32303 9
*	Morning Glory timer
	inst
	mesg wrt+ ownr 1000 0 0 0
endm

scrp 2 4 32303 125
*	Morning Glory lost fruit
	inst
	mesg wrt+ ownr 1000 0 0 rand 20 100
endm


scrp 2 4 32303 1000
*	Morning Glory plant's worker
	inst
	setv va20 0
	epas 2 7 32397
		addv va20 1
	next

	doif va20 < 1 or _p1_ = 1
*		no flower exists, so better make one
		setv va00 posl
		setv va01 post
		new: simp 2 7 32397 "c1plants" 2 21 101
		mira 1
		addv va00 9
		addv va01 9
		mvsf va00 va01
		seta va20 targ
		accg 0
		attr 199
		fric 100
		elas 0
		aero 10
		bhvr 48
		perm 60
		targ ownr
		doif _p1_ <> 1
			setv va52 0
			loop
				spas ownr va20
				wait 1
				inst
				addv va52 1
				targ va20
			untl targ = null or carr = ownr or va52 > 10
			inst
		else
			targ va20
			pose 1
			velo rand -10 10 rand 1 5
		endi
		attr 199
		accg 5
	endi
endm

scrp 2 7 32397 1
*	Morning Glory clicked
	inst
	doif game "C1_Clickable_Food" = 1
		setv va00 posl
		setv va01 post
		new: simp 2 7 32397 "c1plants" 2 21 101
		mira 1
		addv va00 9
		addv va01 9
		seta va20 targ
		accg 5
		attr 199
		fric 100
		elas 0
		aero 10
		bhvr 48
		perm 60
		pose 1
		mira rand 0 1
		mvsf va00 va01
		velo rand -10 10 rand -10 -5
	endi
endm

scrp 2 7 32397 4
*	Morning Glory - I've been picked up
	inst
	lock
	pose 1
	mira rand 0 1
endm

scrp 2 7 32397 12
*	eating a flower
*	Morning Glory
	inst
	lock
	seta va20 norn
	targ from
	doif crea targ = 1
		chem 5 0.9
		chem 154 -1.0
		chem 155 -1.0
*		chem 34 1.0
*		chem 35 1.0
*		chem 125 0.5
		snde "chwp"
		stim writ from 84 1
	endi
	targ ownr
	kill targ
endm

rscr
enum 2 4 32303
	kill targ
next
scrx 2 4 32303 1
scrx 2 4 32303 9
scrx 2 4 32303 125
scrx 2 4 32303 1000

enum 2 7 32397
	kill targ
next
scrx 2 7 32397 4
scrx 2 7 32397 12