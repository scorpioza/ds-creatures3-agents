*Cave Mushrooms*

*-Moe-*



*Spores*

reps rand 5 10

	new: simp 2 3 20003 "lc_mush" 1 0 rand 500 2500

	mvsf rand 8500 9000 rand 9350 9390

repe



scrp 2 3 20003 10

	attr 195

	elas 10

	accg .1

	aero 7

	mira rand 0 1

	tick rand 50 75

endm



*Spore Create



scrp 2 3 20003 9

	doif carr ne null

		stop

	endi



	doif fall eq 1

		addv velx rand -2 2

		addv vely rand 0 -1

	else

		gsub check

	endi



	subr check

*atm it only checks light



		esee 2 8 20000

			doif targ ne null

				addv va03 1

			endi

		next

		targ ownr



		setv va00 prop room ownr 1

		doif va00 lt .5

			doif va03 lt 5

				gsub grow

			else

				wait rand 50 100

				kill ownr

			endi

		endi

	retn



	subr grow

		setv va00 posx

		subv va00 15

		setv va01 posy





		setv va02 rand 0 2



		doif va02 eq 0

			setv va02 1

		elif va02 eq 1

			setv va02 3

		else

			setv va02 5

		endi





		new: simp 2 8 20000 "lc_mush" 2 va02 rand 500 2500

		mvsf va00 va01

		kill ownr

	retn



endm



*Mushroom create

scrp 2 8 20000 10

	attr 195

	accg 3

	elas 0

	aero 3

	bhvr 48

	tick rand 500 700

	mira rand 0 1

endm



scrp 2 8 20000 9

	doif carr eq null

		doif pose lt 1

			pose 1

		endi

		tick 0

	endi

endm



*Mushroom eat

scrp 2 8 20000 12

*create spores

	stim writ from 78 1

	chem 3 .05

	chem 5 .05

	chem 12 .02

	chem 71 .02

	setv va00 posx

	setv va01 posy

	reps rand 1 3

		new: simp 2 3 20003 "lc_mush" 1 0 rand 500 2500

		mvsf va00 va01

		velo rand -2 2 rand 0 -2

	repe

	sndc "chwp"

	kill ownr

endm