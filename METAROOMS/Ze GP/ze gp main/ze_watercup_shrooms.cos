**Watercup Shroom
*Spore timer
scrp 2 3 18885 9
*First, make sure you're on proper soil
	doif rtyp room ownr >= 5 and rtyp room ownr <= 7
*If yes, add one to growth check
		addv ov00 1
	else
*If not, check to see if you're underwater
		doif rtyp room ownr = 8 or rtyp room ownr = 9
*Check to see if you're not completely submerged
			setv va00 grap posx post
			doif rtyp va00 <> 8 and rtyp va00 <> 9
*Add one to grow count if only partially submerged
				addv ov00 1
			else
*Otherwise, rot immediately
				gsub rott
			endi
		endi
	endi

*Now, check to see how many mushrooms are growing nearby
	setv va00 0
	esee 2 4 18885
*Don't count PMs
		doif ov00 <> 4
			addv va00 1
		endi
	next
*If there's less than 5, add to the grow count
	doif va00 < 5
		addv ov00 1
	endi

*Next, check to see how many plants you're touching
	setv va00 0
	etch 2 4 18885
*Don't count PMs
		doif ov00 <> 4
			addv va00 1
		endi
	next
*If you're touching less than 3, add to the grow count
	doif va00 < 3
		addv ov00 1
	endi

*If you've accumulated enough boosts, grow
	doif ov00 = 3
		gsub grow
*Otherwise, wait a while
	else
*Reset the grow count
		setv ov00 0
*If not underwater, go into dormancy
		doif rtyp room ownr <> 8 and rtyp room ownr <> 9
			gsub dorm
*Otherwise, rot
		else
			gsub rott
		endi
	endi

*Growing
	subr grow
*Get positions
		setv va00 posl
		setv va01 post
*Get plane and permability
		setv va02 plne
		setv va03 perm
*Determine what color you grow into
		setv va04 rand 0 2
		inst
	*Teal Shroom
		doif va04 = 0
			setv va06 1
	*Blue Shroom
		elif va04 = 1
			setv va06 6
	*Red Shroom
		else
			setv va06 11
		endi
		setv va08 0
	* Dark variation
		doif rand 0 1 eq 1
*			addv va06 21
			setv va08 1
		endi

		new: simp 2 4 18885 "watercup_shroom" 5 va06 va02
		setv ov01 va04

		* Dark variation
		doif va08 eq 1
			alph rand 50 150 1
		endi

		setv ov04 va08
		attr 196
		bhvr 1
		accg 4
		aero 0
		perm va03
		elas 0
		fric 100
*ov00 = state
		setv ov00 0
*ov02 = cap count
		setv ov02 0
*ov03 = rotted flag
		setv ov03 0
		tick rand 150 450
		mvsf va00 va01
		targ ownr
		kill ownr
	retn

*Dormancy
	subr dorm
*If you've lived long enough, rot
		addv ov01 1

		doif ov01 > 10
			gsub rott
		endi
	retn

*Rotting
	subr rott
*Add nutrients if in a valid room, then die
		doif room ownr <> -1
			altr room targ 4 0.01
		endi
		kill ownr
	retn
endm

*Spore eat
scrp 2 3 18885 12
	doif crea from = 1
		stim writ from 77 1

		* Glucose
			chem 3 0.5
		* Glycogen	
			chem 4 0.5
		* Starch	
			chem 5 0.5		
		* Fatty acid	
			chem 6 0.3
		* Triglyceride	
			chem 8 0.5
		* Adipose tissue: When your creature needs to store fat for the long-term, the fat becomes this. This is the stuff that makes them fat, but they'll never really be obese.	
			chem 9 0.5	
		* Fat	
			chem 10 0.3	
		* Protein	
			chem 12 1.0
		* Amino acid	
			chem 13 0.3		
		* Anti-oxidant:	Cures carbon monoxide poisoning.	
			chem 93 0.5
		* Sodium thiosulphate: Cures cyanide poisoning.	
			chem 96 0.5
		* Arnica: Cures glycotoxin poisoning.	
			chem 97 0.5	
		* Vitamin E: Keeps reproductive organs healthy.			
			chem 98 0.5
		* Vitamin C: Keeps bones healthy, aids in healing from injuries.
			chem 99 0.5	
		* Antihistamine	
			chem 100 0.5	
		* Life: Creature ages as this depletes. A creature dies of old age when it nears zero. Continually injecting this will effectively make your creature immortal.	
			chem 125 0.5		
	endi
	kill ownr
endm

*Spore Exception
scrp 2 3 18885 255
endm

*Mushroom push
scrp 2 4 18885 1
	doif crea from = 1
*Stim the creature with "played with friendly plant
		stim writ from 84 1
	endi
*If producing caps, make one if prodded
*If a PM, use this
	doif ov00 = 4
*If monitoring or actually producing caps
		doif ov02 = 1 or ov02 = 2
*Activate if not so already
			doif pose <> 1
				pose 1
			endi
*Get position
			setv va00 posl
			setv va01 post
			subv va01 18
*Get plane and permability
			setv va02 plne
			addv va02 1
			setv va03 perm
*Determine number of seeds to make by the number lying around
			setv va04 0
			esee 2 3 18885
				addv va04 1
			next
*If there's less than 3, create 4-5
			doif va04 < 3
				setv va10 rand 4 5
*If there's between 3 and 6, create 2-3
			elif va04 >= 3 and va04 < 6
				setv va10 rand 2 3
*If there's between 6 and 9, create 1-2 seeds
			elif va04 >= 6 and va04 < 9
				setv va10 rand 1 2
			else
*Don't bother with the seeds
				setv va10 0
			endi
			inst
*Generate cap

	*If Teal Shroom
			doif mv01 = 0
				setv va06 16
	*If Blue Shroom
			elif mv01 = 1
				setv va06 18
	*If Red Shroom
			else
				setv va06 20
			endi

			doif ov04 eq 1
				addv va06 21
			endi
			new: simp 2 8 18885 "watercup_shroom" 2 va06 va02

			attr 195
			bhvr 48
			accg 2
			aero 0
			perm va03
			elas 0
			fric 50
*ov00 = number of seeds to produce
			setv ov00 va10
			tick rand 150 450
			emit 6 0.1
			mvsf va00 va01
			targ ownr
*Assume normal capless pose
			pose 2
*Prepare to regrow
			setv ov02 3
		endi
	else
		doif ov00 = 1
*Get position
			setv va00 posl
			setv va01 post
			subv va01 18
*Get plane and permability
			setv va02 plne
			addv va02 1
			setv va03 perm
*Determine number of seeds to make by the number lying around
			setv va04 0
			esee 2 3 18885
				addv va04 1
			next
*If there's less than 3, create 4-5
			doif va04 < 3
				setv va10 rand 4 5
*If there's between 3 and 6, create 2-3
			elif va04 >= 3 and va04 < 6
				setv va10 rand 2 3
*If there's between 6 and 9, create 1-2 seeds
			elif va04 >= 6 and va04 < 9
				setv va10 rand 1 2
			else
*Don't bother with the seeds
				setv va10 0
			endi
			inst
*Generate cap
		*If Teal Shroom
			doif mv01 = 0
				setv va06 16
	*If Blue Shroom
			elif mv01 = 1
				setv va06 18
	*If Red Shroom
			else
				setv va06 20
			endi

			doif ov04 eq 1
				addv va06 21
			endi
			new: simp 2 8 18885 "watercup_shroom" 2 va06 va02

			attr 195
			bhvr 48
			accg 2
			aero 0
			perm va03
			elas 0
			fric 50
*ov00 = number of seeds to produce
			setv ov00 va10
			tick rand 150 450
			emit 6 0.1
			mvsf va00 va01
			targ ownr
*Assume dropped cap pose and prepare to regrow
			pose 2
			addv ov02 1
			setv ov00 2
		endi
	endi
endm

*Mushroom timer
scrp 2 4 18885 9
*ov00 is state
*Growing
	doif ov00 = 0
		gsub grow
*Producing caps	
	elif ov00 = 1
		gsub caps
*Regrowing caps
	elif ov00 = 2
		gsub gcap
*Dying
	elif ov00 = 3
		gsub die_
*Population monitor
	elif ov00 = 4
		gsub popm
	endi

*Population monitor state
	subr popm
*ov02 is state here
*0 is growing
		doif ov02 = 0
			gsub grow
*1 is monitoring, but inactive
		elif ov02 = 1
			gsub cpop
*2 is producing caps
		elif ov02 = 2
			gsub caps
*3 is regrowing caps
		elif ov02 = 3
			gsub gcap
		endi
	retn

*Growing
	subr grow
		doif pose <> 1
			pose 1
		else
*If not a PM, simply move on as normal
			doif ov00 <> 4
				setv ov00 1
			else
*Determine whether or not to activate
*Determine the amount of plants, spores, and caps around
				setv va00 0
				setv va10 gmap posx posy
*Spores
				enum 2 3 18885
*Make sure they're in the same area as you
					doif gmap posx posy = va10
						addv va00 1
					endi
				next
*Plants
				enum 2 4 18885
					doif gmap posx posy = va10
*Don't count other PMs
						doif ov00 <> 4
							addv va00 1
						endi
					endi
				next
*Caps
				enum 2 8 18885
					doif gmap posx posy = va10
						addv va00 1
					endi
				next
*If there are 0, remain active and produce a cap
				doif va00 = 0
					setv ov02 2
*Otherwise, deactivate and silently monitor the population
				else
					pose 3
					setv ov02 1
				endi
			endi
		endi
	retn

*Population monitoring
	subr cpop
*Determine the amount of plants, spores, and caps around
		setv va00 0
		setv va10 gmap posx posy
*Spores
		enum 2 3 18885
*Make sure they're in the same area as you
			doif gmap posx posy = va10
				addv va00 1
			endi
		next
*Plants
		enum 2 4 18885
			doif gmap posx posy = va10
*Don't count other PMs
				doif ov00 <> 4
					addv va00 1
				endi
			endi
		next
*Caps
		enum 2 8 18885
			doif gmap posx posy = va10
				addv va00 1
			endi
		next

*If there are no plants or their productions around, prepare to make a cap
		doif va00 = 0
			pose 1
			setv ov02 2
		endi
	retn

*Dropping caps
	subr caps
*Add to cap growth if not a PM
		doif ov00 <> 4
			addv ov02 1
		endi
*Get position
		setv va00 posl
		setv va01 post
		subv va01 18
*Get plane and permability
		setv va02 plne
		addv va02 1
		setv va03 perm
*Determine number of seeds to make by the number lying around
		setv va04 0
		esee 2 3 18885
			addv va04 1
		next
*If there's less than 3, create 4-5
		doif va04 < 3
			setv va10 rand 4 5
*If there's between 3 and 6, create 2-3
		elif va04 >= 3 and va04 < 6
			setv va10 rand 2 3
*If there's between 6 and 9, create 1-2 seeds
		elif va04 >= 6 and va04 < 9
			setv va10 rand 1 2
		else
*Don't bother with the seeds
			setv va10 0
		endi
		inst
*Generate cap
	*If Teal Shroom
		doif mv01 = 0
			setv va06 16
	*If Blue Shroom
		elif mv01 = 1
			setv va06 18
	*If Red Shroom
		else
			setv va06 20
		endi

		doif mv04 eq 1
			addv va06 21
		endi
		new: simp 2 8 18885 "watercup_shroom" 2 va06 va02

		attr 195
		attr 195
		bhvr 48
		accg 2
		aero 0
		perm va03
		elas 0
		fric 50
*ov00 = number of seeds
		setv ov00 va10
		tick rand 150 450
		emit 6 0.1
		mvsf va00 va01
		targ ownr
*Assume dropped cap pose and prepare to regrow
		pose 2
		doif ov00 <> 4
			setv ov00 2
		else
			setv ov02 3
		endi
	retn

*Regrowing caps
	subr gcap
*If you're a PM, regrow your cap, perform a population check, and deactivate if you're not needed
		doif ov00 = 4
			pose 1
*Determine the amount of plants, spores, and caps around
			setv va00 0
			setv va10 gmap posx posy
*Spores
			enum 2 3 18885
*Make sure they're in the same area as you
				doif gmap posx posy = va10
					addv va00 1
				endi
			next
*Plants
			enum 2 4 18885
				doif gmap posx posy = va10
*Don't count other PMs
					doif ov00 <> 4
						addv va00 1
					endi
				endi
			next
*Caps
			enum 2 8 18885
				doif gmap posx posy = va10
					addv va00 1
				endi
			next

*If you're no longer needed, deactivate and continue silently monitoring the population
			doif va00 <> 0
				pose 3
				setv ov02 1
			else
*Produce another cap
				setv ov02 2
			endi

		else
*If you're a normal mushroom who has produced only 1 cap, regrow your cap
			doif ov02 < 2
				pose 1
				setv ov00 1
*Otherwise, prepare to die
			else
				setv ov00 3
			endi
		endi
	retn

*Dying
	subr die_
*Determine if you have a cap if you haven't rotted yet
		doif ov03 = 0
*If you have a cap, assume "rotted with cap on" pose
			doif pose = 1
				pose 3
			else
*Otherwise, rot without your cap
				pose 4
			endi
			setv ov03 1
		else
*Add nutrients to the environment and die
			doif room ownr <> -1
				altr room targ 4 0.05
			endi
			kill ownr
		endi
	retn
endm

*Exception
scrp 2 4 18885 255
endm

*Fruit timer
scrp 2 8 18885 9
*Start rotting if it's been long enough
	doif pose = 0
		pose 1
	else
*Do this only if you're making seeds
		doif ov00 <> 0
*Get positions
			setv va00 posl
			setv va01 post
			inst
*Make spores
			reps ov00
				new: simp 2 3 18885 "watercup_shroom" 1 0 rand 1000 6000
				attr 195
				bhvr 48
				accg 1
				aero 0
				perm rand 30 100
				elas 10
				fric 70
*ov00 = growth checks
				setv ov00 0
*ov01 = dormancy counter
				setv ov01 0
				rnge 250
				tick rand 150 450
				puhl -1 16 31
				emit 7 0.1
				mvsf va00 va01
				velo rand -10 10 rand -10 -1
			repe
		endi
		targ ownr
*Produce nutrients and die
		doif room ownr <> -1
			altr room targ 4 0.02
		endi
		kill ownr
	endi
endm

*Fruit eat
scrp 2 8 18885 12
	sndc "chwp"
*If a creature, stim it with "eaten fruit" depending on pose
	doif crea from = 1
		doif pose = 0
			stim writ from 78 1
		else
			stim writ from 78 0.5
		endi

	* Glucose
		chem 3 0.5
	* Glycogen	
		chem 4 0.5
	* Starch	
		chem 5 0.5		
	* Fatty acid	
		chem 6 0.3
	* Triglyceride	
		chem 8 0.5
	* Adipose tissue: When your creature needs to store fat for the long-term, the fat becomes this. This is the stuff that makes them fat, but they'll never really be obese.	
		chem 9 0.5	
	* Fat	
		chem 10 0.3	
	* Protein	
		chem 12 1.0
	* Amino acid	
		chem 13 0.3		
	* Anti-oxidant:	Cures carbon monoxide poisoning.	
		chem 93 0.5
	* Sodium thiosulphate: Cures cyanide poisoning.	
		chem 96 0.5
	* Arnica: Cures glycotoxin poisoning.	
		chem 97 0.5	
	* Vitamin E: Keeps reproductive organs healthy.			
		chem 98 0.5
	* Vitamin C: Keeps bones healthy, aids in healing from injuries.
		chem 99 0.5	
	* Antihistamine	
		chem 100 0.5	
	* Life: Creature ages as this depletes. A creature dies of old age when it nears zero. Continually injecting this will effectively make your creature immortal.	
		chem 125 0.5

	endi
*Produce spores if you're making seeds
	doif mv00 <> 0
		setv va00 posl
		setv va01 post
		inst
*Make spores
		reps ov00
			new: simp 2 3 18885 "watercup_shroom" 1 0 rand 1000 6000
			attr 195
			bhvr 48
			accg 1
			aero 0
			perm rand 30 100
			elas 10
			fric 70
*ov00 = growth checks
			setv ov00 0
*ov01 = dormancy counter
			setv ov01 0
			rnge 250
			tick rand 150 450
			puhl -1 16 31
			emit 7 0.1
			mvsf va00 va01
			velo rand -10 10 rand -10 -1
		repe
	endi
	targ ownr
	kill ownr
endm