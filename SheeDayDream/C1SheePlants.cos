* 	Feverfew plant
new: vhcl 2 4 32306 "c1plants" 1 17 200
attr 735
cabn 10 -3 40 30
accg 5
fric 100
aero 10
elas 0
bhvr 0
perm 60
tick rand 400 1000
mesg wrt+ targ 1001 0 0 10

mvsf 8432 8993

* 	hide the Feverfew pot
new: simp 1 18 32398 "c1plants" 1 38 0
attr 0
accg 0
fric 100
aero 10
elas 0
bhvr 0
mvto 8431 9013



* C1 Pyrethium

* hide the Pyrethium pot
	new: simp 1 19 32398 "c1plants" 1 39 0
	attr 0
	accg 0
	fric 100
	aero 10
	elas 0
	bhvr 0
	mvto 8568 9003

* Pyrethium plant
	new: vhcl 2 5 32301 "c1plants" 1 6 200
	attr 735
	cabn 10 -3 40 30
	accg 5
	fric 100
	aero 10
	elas 0
	bhvr 0
	perm 60
	mvsf 8572 8953
	tick rand 400 1000
	mesg wrt+ targ 1001 0 0 10





***********
* Scripts

scrp 2 4 32306 1
*	Feverfew plant clicked
	inst
	doif game "C1_Clickable_Food" = 1
		mesg wrt+ targ 1001 1 0 0
	endi
endm

scrp 2 7 32396 1
*	Feverfew weed clicked
	inst
	lock
	doif game "C1_Clickable_Food" = 1
		setv va00 posl
		setv va01 post
		new: simp 2 7 32396 "c1plants" 2 18 101
		seta va20 targ
		fric 100
		aero 10
		elas 0
		bhvr 48
		perm 60
		pose 1
		accg 3
		attr 199
		aero 10
		mvsf va00 va01
		velo rand -15 15 rand -15 -5
	endi
endm

scrp 2 4 32306 9
*	Feverfew plant timer
	inst
	doif movs = 0
		mesg wrt+ targ 1001 0 0 0
	endi
endm


scrp 2 4 32306 125
*	Feverfew weed lost flower
	inst
	mesg wrt+ ownr 1001 0 0 rand 10 100
endm


scrp 2 4 32306 1001
*	Feverfew plant's stuff
	inst
	doif movs = 0
		setv va20 0
		epas 2 7 32396
			addv va20 1
		next
		doif va20 < 1 or _p1_ <> 0
*		no flower exists, so better make one
			setv va00 posl
			setv va01 post
			new: simp 2 7 32396 "c1plants" 2 18 101
			addv va00 10
			addv va01 10
			seta va20 targ
			fric 100
			aero 10
			elas 0
			bhvr 48
			perm 60
			mvsf va00 va01
			targ ownr
			doif _p1_ <> 1
*			growing
				targ va20
				setv va21 0
				loop
					targ ownr
					spas ownr va20
					wait 1
					inst
					targ va20
					addv va21 1
				untl carr = ownr or va21 > 100
				targ va20
			else
*			vending		
				targ va20
				pose 1
				velo rand -10 10 rand 1 5
			endi
			targ va20
			accg 3
			attr 199
			fric 90
			aero 10
		endi
	else
		mesg wrt+ ownr 1001 0 0 rand 10 40
	endi
endm

scrp 2 7 32396 4
*	Feverfew picked up
	pose 1
endm

scrp 2 7 32396 12
*	eating a weed
*	Feverfew
	inst
	lock
	seta va20 norn
	targ from
	doif crea targ = 1
		chem 148 -1.0
		chem 149 -1.0
		chem 150 -1.0
		chem 151 -1.0
		chem 5 0.9
*		chem 34 1.0
*		chem 90 -1.0
*		chem 99 0.5
*		chem 100 0.5
		snde "chwp"
		stim writ from 84 1
	endi
	targ ownr
	kill targ
endm

*******************************************


scrp 2 5 32301 1
*	Pyrethium plant clicked
	inst
	mesg wrt+ targ 1001 1 0 0
endm

scrp 2 5 32392 1
*	Pyrethium weed clicked
	inst
	lock
	rtar 2 5 32301
	doif targ <> null
		mesg wrt+ targ 1001 1 0 0
	endi
endm

scrp 2 5 32301 9
*	Pyrethium plant timer
	inst
	doif movs = 0
		mesg wrt+ targ 1001 0 0 0
	endi
endm


scrp 2 5 32301 125
*	Pyrethium weed lost flower
	inst
	mesg wrt+ ownr 1001 0 0 rand 10 100
endm


scrp 2 5 32301 1001
*	Pyrethium plant's stuff
	inst
	doif movs = 0
		setv va20 0
		epas 2 5 32392
			addv va20 1
		next
		doif va20 < 1 or _p1_ <> 0
*		no flower exists, so better make one
			setv va00 posl
			setv va01 post
			new: simp 2 5 32392 "c1plants" 2 7 101
			addv va00 10
			addv va01 10
			seta va20 targ
			fric 100
			aero 10
			elas 0
			bhvr 48
			perm 60
			mvsf va00 va01
			targ ownr
			doif _p1_ <> 1
*			growing
				targ va20
				setv va21 0
				loop
					targ ownr
					spas ownr va20
					wait 1
					inst
					targ va20
					addv va21 1
				untl carr = ownr or va21 > 100
				targ va20
			else
*			vending		
				targ va20
				pose 1
				velo rand -10 10 rand 1 5
			endi
			targ va20
			accg 3
			attr 199
			fric 90
			aero 10
		endi
	else
		mesg wrt+ ownr 1001 0 0 rand 10 40
	endi
endm

scrp 2 5 32392 4
*	Pyrethium picked up
	pose 1
endm

scrp 2 5 32392 12
*	eating a weed
*	Pyrethium
	inst
	lock
	seta va20 norn
	targ from
	doif crea targ = 1

		* Energy. 
			chem 34 1.0
		* Injury
			chem 127 -1.0
		* Hunger for carb
			chem 150 -1.0
		* Glucose
			chem 3 0.5
		* Glycogen	
			chem 4 0.5
		* Starch	
			chem 5 0.5		
		* Fatty acid	
			chem 6 0.3
		* Triglyceride	
			chem 8 0.5
		* Adipose tissue: When your creature needs to store fat for the long-term, the fat becomes this. This is the stuff that makes them fat, but they'll never really be obese.	
			chem 9 0.5	
		* Fat	
			chem 10 0.3	
		* Protein	
			chem 12 1.0
		* Amino acid	
			chem 13 0.3		
		* Anti-oxidant:	Cures carbon monoxide poisoning.	
			chem 93 0.5
		* Sodium thiosulphate: Cures cyanide poisoning.	
			chem 96 0.5
		* Arnica: Cures glycotoxin poisoning.	
			chem 97 0.5	
		* Vitamin E: Keeps reproductive organs healthy.			
			chem 98 0.5
		* Vitamin C: Keeps bones healthy, aids in healing from injuries.
			chem 99 0.5	
		* Antihistamine	
			chem 100 0.5	
		* Life: Creature ages as this depletes. A creature dies of old age when it nears zero. Continually injecting this will effectively make your creature immortal.	
			chem 125 0.5
			
		snde "chwp"
		stim writ from 83 1
	endi
	targ ownr
	kill targ
endm



rscr

enum 1 18 32398
	kill targ
next

enum 2 4 32306
	kill targ
next
scrx 2 4 32306 1
scrx 2 4 32306 9
scrx 2 4 32306 125
scrx 2 4 32306 1001



enum 2 7 32396
	kill targ
next
scrx 2 7 32396 1
scrx 2 7 32396 4
scrx 2 7 32396 12