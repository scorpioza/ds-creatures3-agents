* Script 1 extracted from FoodFountain.agents by Mirality REVELATION

* Script 1 extracted from FOOD_F~1.AGE by Mirality REVELATION



setv va00 game "CreatorX"
setv va01 game "CreatorY"
new: simp 2 23 32102 "food_fountain" 2 0 5000
attr 198
bhvr 32
perm 90
accg 2
fric 100
elas 50
aero 0
mvsf va00 va01
frat 2
velo rand 30 40 -5


***Switch on Switch off
scrp 2 23 32102 1111
	clac 1111
	sndc "bep2"
	base 0
	doif pose eq 0
		pose 1
		tick 100
	elif pose eq 1

		pose 0
		tick 0
	endi
endm


**PUSH SCRIPT - fountain
scrp 2 23 32102 1
	tick 0
	stim writ from 90 1
	setv va00 posl
	addv va00 62
	setv va01 post
	addv va01 1



	inst
*Cooookie...Mmmm
	new: simp 2 11 32103 "food_fountain" 1 3 5001
	attr 195
	bhvr 48
	elas 30
	perm 60
	fric 50
	accg 3
	seta va04 targ
	setv ov61 10
	emit 8 0.35
	tick 2000
	doif tmvt va00 va01 = 1
		mvto va00 va01

		velo rand -25 25 rand -5 -20

	elif tmvt va00 va01 = 0
		targ va04
		kill targ
		targ ownr
	endi


*A very blue berry

	inst
	new: simp 2 8 32103 "food_fountain" 1 2 5001
	attr 195
	bhvr 48
	elas 30
	accg 5
	fric 80
	perm 60
	seta va05 targ
	setv ov61 8
	emit 6 0.5
	tick 200
	doif tmvt va00 va01 = 1
		mvto va00 va01
		velo rand -25 25 rand -5 -20

	elif tmvt va00 va01 = 0

		targ va05
		kill targ
		targ ownr
	endi


**Seeds
	reps 2
		inst
		new: simp 2 3 32103 "food_fountain" 1 4 5001
		attr 195
		accg 5

		bhvr 48
		elas 50
		fric 90

		perm 60
		seta va06 targ
		setv ov61 15
		emit 7 0.5
		tick 250
		doif tmvt va00 va01 = 1

			mvto va00 va01

			velo rand -25 25 rand -5 -20

		elif tmvt va00 va01 = 0
			targ va06
			kill targ
			targ ownr
		endi
	repe
	targ ownr
	doif pose eq 1
		tick 100
	elif pose eq 0
		tick 0
	endi
endm


**EAT SCRIPT - Cookie
scrp 2 11 32103 12
	lock
	sndc "chwp"
	stim writ from 79 1
	wait 2
	kill ownr
endm

***EAT SCRIPT - Berry
scrp 2 8 32103 12
	lock
	snde "eat1"
	stim writ from 78 1
	wait 2
	kill ownr
endm

**DROP SCRIPT - apple
scrp 2 8 32103 6
	snde "Dr10"
endm


**EAT SCRIPT - bean
scrp 2 3 32103 12
	lock
	sndc "chwp"
	stim writ targ 77 1
	wait 2
	kill ownr
endm


**Make noise when dropped - bean
scrp 2 3 32103 6
	snde "clak"
endm


**Make noise when dropped - cheese
scrp 2 11 32103 6
	snde "Dr10"
endm

**Make noise when dropped - fountain
scrp 2 23 32102 6
	snde "Clik"
endm

***Cookie Tick
scrp 2 11 32103 9
	setv va10 rand 50 400
	doif carr eq null
		wait va10
		kill ownr
	endi
endm

*** Tick
scrp 2 3 32103 9
	setv va10 rand 50 400
	doif carr eq null
		wait va10
		kill ownr
	endi
endm

***Tick
scrp 2 8 32103 9
	setv va10 rand 50 400
	doif carr eq null
		wait va10
		kill ownr
	endi
endm



**Fountain Timer
scrp 2 23 32102 9
	setv va08 rand 100 200
	wait va08
	stim writ from 90 1
	setv va00 posl
	addv va00 62
	setv va01 post
	addv va01 1



	inst
*Cooookie...Mmmm
	new: simp 2 11 32103 "food_fountain" 1 3 5001
	attr 195
	bhvr 48
	elas 30
	perm 60
	fric 50
	accg 3
	seta va04 targ
	setv ov61 10
	emit 8 0.35
	tick 200
	doif tmvt va00 va01 = 1
		mvto va00 va01

		velo rand -25 25 rand -5 -20

	elif tmvt va00 va01 = 0
		targ va04
		kill targ
		targ ownr
	endi


*A very blue berry

	inst
	new: simp 2 8 32103 "food_fountain" 1 2 5001
	attr 195
	bhvr 48
	elas 30
	accg 5
	fric 80
	perm 60
	seta va05 targ
	setv ov61 8
	emit 6 0.5
	tick 200
	doif tmvt va00 va01 = 1
		mvto va00 va01
		velo rand -25 25 rand -5 -20

	elif tmvt va00 va01 = 0

		targ va05
		kill targ
		targ ownr
	endi


**Seeds
	reps 4
		inst
		new: simp 2 3 32103 "food_fountain" 1 4 5001
		attr 195
		accg 5

		bhvr 48
		elas 50
		fric 90

		perm 60
		seta va06 targ
		setv ov61 15
		emit 7 0.5
		tick 250
		doif tmvt va00 va01 = 1

			mvto va00 va01

			velo rand -25 25 rand -5 -20

		elif tmvt va00 va01 = 0
			targ va06
			kill targ
			targ ownr
		endi
	repe
	tick va08
endm

***REMOVAL SCRIPT
rscr
enum 2 23 32102
	kill targ
next
enum 2 11 32103
	kill targ
next
enum 2 8 32100
	kill targ
next
enum 2 3 32103
	kill targ
next
scrx 2 8 32103 9
scrx 2 11 32103 9
scrx 2 3 32103 9
scrx 2 23 32103 9
scrx 2 8 32103 12
scrx 2 23 32102 1
scrx 2 11 32103 12
scrx 2 3 32103 12
scrx 2 3 32103 6
scrx 2 8 32103 6
scrx 2 11 32103 6
scrx 2 23 32102 6