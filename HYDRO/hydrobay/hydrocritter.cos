
inst
new: simp 1 17 60853 "blnk" 2 0 0
accg 0
perm 0
tick 400
attr 0
anim [0]

mvto 8100 2780

reps 8
	inst
	new: simp 2 15 60853 "albiansnail" 16 0 3500
	attr 194
	bhvr 59
	elas 99
	accg 3
	aero 10
	fric 70
	perm 60
	tick 16
	doif tmvt 8100 2780 eq 1
		mvto 8100 2780
	endi
repe

scrp 1 17 60853 9
	inst
	rnge 800
	esee 2 16 6
		addv va00 1
	next
*if less than or equal to 3, and total of critters under 15, make one more.
	doif va00 <= 3 and totl 2 15 60853 < 15
		new: simp 2 15 60853 "albiansnail" 16 0 3500
		attr 194
		bhvr 59
		elas 99
		accg 3
		aero 10
		fric 70
		perm 60
		tick 16
		doif tmvt 8100 2780 eq 1
			mvto 8100 2780
		endi
	endi
endm


scrp 2 15 60853 9
	addv ov99 1
	doif ov99 eq rand 500 1500
		kill ownr
	endi
	setv va00 rand 0 1
	doif va00 eq 0
		reps rand 5 17
			anim [0 1 2 3 4 5 6 7]
			over
			velo 6 -2
		repe
	else
		reps rand 5 17
			anim [8 9 10 11 12 13 14 15]
			over
			velo -6 -2
		repe
	endi
endm

**********************
***Collision Script***
**********************
scrp 2 15 60853 6
	doif wall eq rght
		velo -6 -2
		setv ov00 0
	elif wall eq left
		velo 6 -2
		setv ov00 50
	endi
endm

**********************
***   Push Script  ***
**********************
scrp 2 15 60853 1
	targ from
	stim writ targ 85 1
endm

**********************
***   Pull Script  ***
**********************
scrp 2 15 60853 2
	targ from
	stim writ targ 85 1
endm

**********************
***   Hit Script   ***
**********************
scrp 2 15 60853 3
	inst
*	snde "spnk"
	doif from <> null
		targ from
		doif crea targ = 1
* REDUCE Pain, Fear and Anger!
			driv 0 -0.45
			driv 10 -0.35
			driv 12 -0.35
		endi
	endi
	kill ownr
endm
**********************
***   Eat Script  ***
**********************
scrp 2 15 60853 12
	targ from
	chem 12 1.0
	chem 4 1.0
	stim writ targ 80 1
	kill ownr
endm


rscr
enum 2 15 60853
	kill targ
next
scrx 2 15 60853 9
scrx 2 15 60853 6
scrx 2 15 60853 1
scrx 2 15 60853 2
scrx 2 15 60853 3
scrx 2 15 60853 12
