* Cable Car

* Dependencies:
*  sheedaydream_car.c16
*  sheedaydream_carPlatform.c16
*  c1Lf.wav


* BUG:  When creatures push or pull lift, they pop to the top of the car briefly!!

* right:    10015 8476
* left:     8280 8476
* Slope: -0.0473292765
* post =(-0.0473292765382015*posl)+10573.4847870183

inst
doif totl 3 1 62313 < 1

	setv game "C1_Cable_Car_Speed" 7

	new: simp 1 1 62313 "blnk" 1 0 0
	attr 0
	bhvr 0
	accg 0
	fric 100
	aero 100
	tick rand 700 900

    * mvto 10040 8520
	mvto 10015 8476

	new: vhcl 3 1 62313 "sheedaydream_car" 1 0 1000
	setv ov97 8280
	setv ov98 10015
	setv ov99 0
	cabp -999
	cabn 30 0 136 226
*	cabn -60 -30 176 191

*	pat: dull 1 "sheedaydream_car" 1 17 74 -1000
*	pat: dull 2 "sheedaydream_car" 2 42 129 2
	pat: dull 1 "sheedaydream_car" 1 42 115 -1000
	pat: dull 2 "sheedaydream_car" 2 67 170 2
	part 2
	pose 2
	attr 516
	pat: butt 3 "sheedaydream_car" 5 2 38 52 1 [] 0 0
	part 3
	pose 1
	pat: butt 4 "sheedaydream_car" 7 2 104 52 1 [] 1 0
	part 4
	pose 1
	accg 0
	aero 100
	fric 100
	bhvr 3
	elas 0
	perm 0
	tick 1
	clac 0
	setv ov61 100
	mvto 10015 8476
endi
scrp 1 1 62313 9
*	cable car platform timer
	rtar 3 1 62313
	doif ov99 = 0 and targ <> null
		doif posl > 4696
			* sndl "c1lf"
			mesg writ targ 1
		else
			* sndl "c1lf"
			mesg writ targ 2
		endi
	endi
endm

scrp 3 1 62313 1
*	push script
	inst
	lock
	stim writ from 94 1.0
	* sndl "c1lf"
	doif ov99 = 0 or ov99 = -2
*		currently not going anywhere
*		hide the cable car from creatures
		attr 540
		mesg wrt+ ownr 2003 from 0 0

		doif posl <= ov98 and posl <> ov97
			setv ov99 -1
		elif posl <> ov98 and posl >= ov97
			setv ov99 1
		endi
	elif ov99 <> 0 and ov99 <> -2
		targ ownr
		doif ov99 = 1
			setv ov99 -1
		elif ov99 = -1
			setv ov99 1
		endi
	endi
	doif ov99 <> 0
		targ ownr
		tick 1
	endi
endm

scrp 3 1 62313 2
*	pull script
	stim writ from 94 1.0
	mesg writ ownr 0
endm

scrp 3 1 62313 2001
	inst
	lock
	doif posl > ov97 and ov99 = -1
*		let's go left
		setv va00 posl
		subv va00 game "C1_Cable_Car_Speed"
		mvto va00 8476
	elif posl <= ov97 and ov99 = -1
		setv ov99 0
		setv va00 ov97
		mvto va00 8476
		stpc
		mesg writ ownr 3002
	endi
endm

scrp 3 1 62313 2002
	inst
	lock
	doif posl < ov98 and ov99 = 1
*		let's go right
		setv va00 posl
		addv va00 game "C1_Cable_Car_Speed"
		mvto va00 8476
	elif posl >= ov98 and ov99 = 1
		setv ov99 0
		setv va00 ov98
		mvto va00 8476
		stpc
		mesg writ ownr 3002
	endi
endm

scrp 3 1 62313 2003
*	pickup creatures
	inst
	setv va00 posx
	setv va01 posb
	subv va01 15
*	subv va00 30
	doif type _p1_ = 7
		setv va21 0
		targ _p1_
*		setv va01 post
		loop
*				mvsf va00 va01
			mvft va00 va01
			spas ownr _p1_
			wait 1
			inst
			addv va21 1
		untl movs <> 0 or va21 > 100
	else
		etch 4 0 0
			doif movs = 0
				mvft va00 va01
			endi
		next
		gpas 4 0 0 0
	endi
	targ ownr
	epas 4 0 0
		doif zomb <> 1
			zomb 1
		endi
		setv va21 0
		loop
			pose 74
			dirn 1
			wait 1
			inst
			addv va21 1
		untl dirn = 1 or va21 > 100
	next
endm

scrp 3 1 62313 3002
*	drop creatures
	inst
	lock
	attr 516
	setv va03 posb
	subv va03 15
	doif posl <= ov97
		setv va02 posl
		setv va00 -5
	elif posl >= ov98
		setv va02 posr
		setv va00 5
	endi
	epas 4 0 0
		rpas ownr targ
		mvft va02 va03
		velo va00 -5
		zomb 0
	next
*	dpas 4 0 0	
endm


scrp 3 1 62313 9
	inst
	lock
	doif ov99 = -1
		attr 540
		mesg writ ownr 2001
		part 2
		pose 0
		part 3
		pose 0
		part 4
		pose 1
	elif ov99 = 1
		attr 540
		mesg writ ownr 2002
		part 2
		pose 1
		part 3
		pose 1
		part 4
		pose 0
	elif ov99 = 0
		stpc
		attr 516
		part 2
		pose 2
		part 3
		pose 1
		part 4
		pose 1
		tick rand 700 900
		setv ov99 -2
	elif ov99 = -2
		mesg wrt+ ownr 2003 null 0 0
		doif posl = ov97
			* sndl "c1lf"
			setv ov99 1
		elif posl = ov98
			* sndl "c1lf"
			setv ov99 -1
		endi
		tick 1
	endi
endm

rscr
enum 3 1 62313
	kill targ
next
enum 1 1 62313
	kill targ
next
scrx 3 1 62313 1
scrx 3 1 62313 2
scrx 3 1 62313 1001
scrx 3 1 62313 1002
scrx 3 1 62313 2001
scrx 3 1 62313 2002
scrx 3 1 62313 2003
scrx 3 1 62313 3001
scrx 3 1 62313 3002