*tribble
*tribblenest 2 17 31932
*tribble 2 15 31932
*tribbleeg 2 18 31932


**************************
*tribblenest 2 17 31932
*ov00 stage/pose 0-3 growing 4 inhabitated

*tickscript
scrp 2 17 31932 9
*ov81 number of small black tribbles inside
*ov82 number of small brown tribbles inside
*ov83 number of small beige tribbles inside
*ov84 number of small white tribbles inside
*ov85 number of med black tribbles inside
*ov86 number of med brown tribbles inside
*ov87 number of med beige tribbles inside
*ov88 number of med white tribbles inside
*ov89 number of big black tribbles inside
*ov90 number of big brown tribbles inside
*ov91 number of big beige tribbles inside
*ov92 number of big white tribbles inside
doif ov00 < 3
	pose ov00
	addv ov00 rand 0 1
	stop
elif ov00 = 3
	pose 3
	setv va02 0
	rnge 1200
	esee 2 15 31932
		setv va02 1
	next
	doif va02 = 0
		setv va02 rand 2 6
	endi
	doif va02 = 2
		addv ov81 1
		setv ov00 4
	endi
endi

*look it touch a tribble
setv va02 0
seta va12 null
inst
etch 2 15 31932
*look what kind of tribble we have
	setv va02 1

	setv va10 ov00
	doif targ ne null and va10 < 100
		seta va12 targ
	endi
	setv va15 ov05
next
slow
wait rand 30 60

doif va12 ne null
	kill va12
	setv va02 3
endi
*store info about the tribble
doif va02 = 3
	*tribble is small
		doif va10 < 70
			doif va15 = 0
				addv ov81 1
			elif va15 = 16
				addv ov82 1
			elif va15 = 32
				addv ov83 1
			elif va15 = 48
				addv ov84 1
			endi
*tribble is medium
		elif va10 >= 70 and va10 < 80
			doif va15 = 0
				addv ov85 1
			elif va15 = 16
				addv ov86 1
			elif va15 = 32
				addv ov87 1
			elif va15 = 48
				addv ov88 1
			endi
*tribble is big-huge tribbles cannot get in
		elif va10 >= 80 and va10 < 100
			doif va15 = 0
				addv ov89 1
			elif va15 = 16
				addv ov90 1
			elif va15 = 32
				addv ov91 1
			elif va15 = 48
				addv ov92 1
			endi
		endi
		setv ov00 4
*not touching a tribble-produce one of the stored tribbles
elif va02 = 0 and ov00 > 2
	setv va03 0
	doif ov92 > 0
		setv va03 rand 0 1
		setv va15 48
		setv va10 80
		doif va03 = 1
			setv va16 92
		endi
	endi
	doif ov91 > 0 and va03 = 0
		setv va03 rand 0 1
		setv va15 32
		setv va10 80
		doif va03 = 1
			setv va16 91
		endi
	endi
	doif ov90 > 0 and va03 = 0
		setv va03 rand 0 1
		setv va15 16
		setv va10 80
		doif va03 = 1
			setv va16 90
		endi
	endi
	doif ov89 > 0 and va03 = 0
		setv va03 rand 0 1
		setv va15 0
		setv va10 80
		doif va03 = 1
			setv va16 89
		endi
	endi
	doif ov88 > 0 and va03 = 0
		setv va03 rand 0 1
		setv va15 48
		setv va10 70
		doif va03 = 1
			setv va16 88
		endi		
	endi
	doif ov87 > 0 and va03 = 0
		setv va03 rand 0 1
		setv va15 32
		setv va10 70
		doif va03 = 1
			setv va16 87
		endi
	endi
	doif ov86 > 0 and va03 = 0
		setv va03 rand 0 1
		setv va15 16
		setv va10 70
		doif va03 = 1
			setv va16 86
		endi
	endi
	doif ov85 > 0 and va03 = 0
		setv va03 rand 0 1
		setv va15 0
		setv va10 70
		doif va03 = 1
			setv va16 85
		endi
	endi
	doif ov84 > 0 and va03 = 0
		setv va03 rand 0 1
		setv va15 48
		setv va10 60
		doif va03 = 1
			setv va16 84
		endi		
	endi
	doif ov83 > 0 and va03 = 0
		setv va03 rand 0 1
		setv va15 32
		setv va10 60
		doif va03 = 1
			setv va16 83
		endi
	endi
	doif ov82 > 0 and va03 = 0
		setv va03 rand 0 1
		setv va15 16
		setv va10 60
		doif va03 = 1
			setv va16 82
		endi
	endi
	doif ov81 > 0 and va03 = 0
		setv va03 1
		setv va15 0
		setv va10 60
		setv va16 81
	endi
	doif va03 = 1
		setv va02 0
		doif va10 = 70
			setv va02 64
		elif va10 = 80
			setv va02 128
		elif va10 = 90
			setv va02 192
		endi 

		inst
	*make a new tribble
		setv va00 posl
		setv va01 post
		new: simp 2 15 31932 "ngtripple" 64 va02 501
			attr 199
			bhvr 59
			elas 30
			fric 95
			accg 3
			aero 2
			perm 60
			setv ov05 va15	
			setv ov00 va10
			setv ov01 0
			setv ov02 50
			setv ov10 0
			setv ov98 3
			base ov05
			anim [13 14 255]
			tick rand 10 50
			velo rand -10 10 rand -6 -1
			mvsf va00 va01
		targ ownr
		slow
		doif va16 = 81
			subv ov81 1
		elif va16 = 82
			subv ov82 1
		elif va16 = 83
			subv ov83 1
		elif va16 = 84
			subv ov84 1
		elif va16 = 85
			subv ov85 1
		elif va16 = 86
			subv ov86 1
		elif va16 = 87
			subv ov87 1
		elif va16 = 88
			subv ov88 1
		elif va16 = 89
			subv ov89 1
		elif va16 = 90
			subv ov90 1
		elif va16 = 91
			subv ov91 1
		elif va16 = 92
			subv ov92 1
		endi
	endi
	doif ov81 <= 0 and ov82 <= 0 and ov83 <= 0 and ov84 <= 0 and ov85 <= 0 and ov86 <= 0 and ov87 <= 0 and ov88 <= 0 and ov89 <= 0 and ov90 <= 0 and ov91 <= 0 and ov92 <= 0 
		setv ov00 3
	endi
	
endi
tick rand 100 500
endm

*pushscript
scrp 2 17 31932 1
doif pose > 0 and pose <= 3
	setv va03 pose
	subv va03 1
	pose va03
	setv ov00 4
	tick 500
else
	kill targ
endi
	
endm

*pullscript
scrp 2 17 31932 2
doif pose > 0 and pose <= 3
	setv va03 pose
	subv va03 1
	pose va03
	setv ov00 4
else
	kill targ
endi

endm

*hitscript
scrp 2 17 31932 2
inst
targ from
doif targ <> null
	driv 0 -0.45
	driv 10 -0.35
	driv 12 -0.35
endi
targ ownr
doif pose > 0 and pose <= 3
	setv va03 pose
	subv va03 1
	pose va03
	setv ov00 4
else
	kill targ
endi

endm
********** pointerscript
scrp 2 17 31932 101
	inst
	frat 1
	anim [10 11 10 0]
endm

*false targ script
scrp 2 17 31932 255
stop
endm

*****************************

*tribblescripts
*ov00 size (energylevel to grow 60/70/80/100)
*ov01 age 
*ov02 energy
*ov05 color (base)
*ov10 orientation(horizontal not sexual)
*ov95 last mates color
*ov98 in water if 1 nestbuilding if 2 fresh out ov nest if 3
*ov99 eggcountdown

	

*tickscript
scrp 2 15 31932 9

*age and consume energy
addv ov01 1
subv ov02 1

*if too old - die
doif ov01 > 600 or ov02 <= 0
	gsub die
endi

*set  base for all animations
base ov05

*what to do if in water or having been in water
doif rtyp room targ = 8 or rtyp room targ = 9
	doif ov98 ne 1
		setv ov98 1
		doif ov10 = 1
			setv ov10 -1
		else
			setv ov10 1
		endi 
		accg 1
		aero 20
	endi
endi 
doif ov98 = 1
	doif rtyp room targ = 8 or rtyp room targ = 9
		gsub swim
	else
		accg 1
		aero 5
		addv ov96 1
		doif ov96 = 5
			doif rtyp room targ = 8 or rtyp room targ = 9
				gsub swim
			else
				setv ov98 0
				setv ov96 0
				accg 3
				aero 1
			endi
		elif ov96 > 20
			gsub die
		endi
	endi
	tick 1
elif ov98 = 2
*if in nestbuilding mode
	gsub front
	reps rand 3 6
		velo 0 rand -1 -9
		wait rand 8 15
	repe
	doif ov16 ne null
		targ ov16
		doif ov00 < 3
			addv ov00 1
		else
			setv va98 1
		endi
		targ ownr
		doif va98 = 1
			setv ov98 0
		endi
	else
		setv ov98 0
	endi

else
	
	*if energy is high-grow or lay egg
	doif ov02 > ov00
		doif ov00 = 100  
			
****************check if a nest in the room and produce one if not
			rnge 900
			setv va04 0
			esee 2 17 31932
				setv va04 1
			next
*if nest present
			doif va04 = 1 and ov99 > 0
				gsub egg
			elif va04 = 0
*if not produce nest
				setv va00 posl
				setv va01 posy
				seta ov16 null
				seta va16 null
				new: simp 2 17 31932 "ngtribblenest" 4 0 450
					bhvr 11
					attr 192
					clac 0
					accg 1
					perm 60
					elas 0
					fric 100
					tick rand 100 500
					mvsf va00 va01
					seta va16 targ
				targ ownr
				setv ov98 2
				doif va16 ne null
					seta ov16 va16
				endi
			endi



		elif ov00 < 100
			gsub grow
		endi
	
	endi 

	*check if touching other tripple or seed or nest

	setv va04 0
	setv va05 0
	setv va06 0
	setv va99 0
	seta va10 null
	inst
	etch 2 15 31932
		doif targ ne ownr
			setv va04 1
			doif ov00 = 100
				setv va06 1
				setv va95 ov05
			endi
		endi
	next
	doif ov98 ne 3
		etch 2 17 31932
			setv va04 2
			tick 1
		next
	endi
	etch 2 3 0
		setv va05 1
		seta va10 targ
	next
	slow
	*remember that other tripples color
	setv ov95 va95
	*eat if food available-kisspop if tripple is available
	doif va04 = 1 and ov02 > 40

		doif va06 = 1
			setv ov99 rand 5 20
		endi
		gsub kisspop 
		gsub jump
	*if touching a nest and not to big to get in
	elif va04 = 2 and ov00 < 100
		gsub kisspop 
	else
		doif va05 = 1
			gsub eat
		else
			gsub jump
		endi
	endi


	*random tick
	tick rand 30 200
endi

subr jump
	inst
	doif ov10 = 0
		setv ov10 rand 0 1
		doif ov10 = 0
			setv ov10 -1
		endi
		doif ov10 > 0
			anim [5]
			over
		elif ov10 < 0
			anim [11]
			over
		endi
	endi
	setv va05 rand 3 8
	mulv va05 ov10
	doif ov10 > 0
		anim [0 1 2 3]
		over
		velo va05 rand -5 -15
		anim [4 3 2 1]
		over
		anim [0 1 255]
	elif ov10 < 0
		anim [6 7 8 9]
		over
		velo va05 rand -5 -15
		anim [10 9 8 7]
		over
		anim [6 7 255]
	endi
retn

subr swim

*if falling into water try getting out of it 
	inst
	aero 1
	setv va10 ov10
	mulv va10 rand 10 20
	velo va10 0
	gsub jump
	gsub moove

retn

subr grow
	inst
	setv va00 posl
	subv va00 5
	setv va01 post
	subv va01 40
	doif ov00 = 60
		setv va02 64
	elif ov00 = 70
		setv va02 128
	elif ov00 = 80
		setv va02 192
	elif ov00 = 90
		setv va02 192
	endi
	setv va05 ov05
	doif va05 ne 0 and va05 ne 16 and va05 ne 32 and va05 ne 48
		setv va05 0
	endi 
	setv va06 ov00
	addv va06 10
	setv va07 ov01
	setv va09 0
	snde "tpl2"
	new: simp 2 15 31932 "ngtripple" 64 va02 501
		attr 199
		bhvr 59
		elas 30
		fric 95
		accg 3
		aero 2
		perm 60
		setv ov05 va05	
		setv ov00 va06
		setv ov01 va07
		setv ov02 50
		setv ov10 0
		base ov05
		anim [13 14 255]
		tick rand 10 30
		velo 0 rand -2 -5
		doif tmvt va00 va01 = 1
			mvto va00 va01
		else
			kill targ
			setv va09 1
		endi
	targ ownr
	doif va09 ne 1
		kill ownr
	endi
retn

subr egg
	inst
	setv va07 rand 3 5
	doif va07 = 5
		gsub front
		lock
		reps rand 3 6
			velo 0 rand -1 -9
			wait rand 8 15
		repe
		inst
		lock
		setv va95 rand 0 1
		doif va95 = 0
			setv va09 ov05
		else
			setv va09 ov95
		endi
		setv va08 rand 0 6
		doif va08 = 0
			addv va09 16
		elif va08 = 1
			subv va09 16
		endi
		doif va09 < 0
			setv va09 ov05
		elif va09 > 48
			setv va09 ov05
		endi
		setv va00 posl
		addv va00 15
		setv va01 post
		subv va01 5
		setv va11 rand 0 250
		setv va12 rand 0 250
		setv va13 rand 0 250
		setv va14 rand 0 250
		setv va15 rand 0 250
		snde "tpl2"
		new: simp 2 18 31932 "ngtripple" 3 256 500
			attr 195
			bhvr 48
			elas 30
			fric rand 70 90
			accg 3
			perm rand 50 70
			setv ov05 va09
			tick rand 300 1200

			doif tmvt va00 va01 = 1
				mvto va00 va01
				tint va11 va12 va13 va14 va15
			else
				kill targ
			endi
		targ ownr
		subv ov99 1
		subv ov02 5
		gsub moove
	endi
retn

subr kisspop
	gsub front
	reps rand 3 5
		velo 0 rand -3 -10
		setv va11 rand 0 6
		doif va11 = 0
			snde "tpl5"
		elif va11 = 1
			snde "tpl6"
		elif va11 = 2
			snde "tpl4"
		endi
		wait rand 9 15
	repe
retn
subr eat
	gsub front
	reps 5
		anim [12 13 14]
		over
	repe
	inst
	doif va10 ne null
		setv va11 0
		targ va10
		doif targ ne null
			doif ov66 = 66
				mesg writ targ 12
			else
				kill targ
			endi
			setv va11 rand 10 30
		endi
		targ ownr
		addv ov02 va11
	endi
	reps 5
		anim [12 13 14]
		over
	repe
	gsub moove
retn

subr die
	lock
	gsub front
	gsub moove
	wait rand 5 50
	anim []
	pose 15
	wait rand 50 500
	inst
	setv va00 posx
	setv va01 posy
	setv va02 ov00
	divv va02 10
	snde "tpl3"
	reps va02
		new: simp 2 10 31933 "ngtripple" 1 259 400
		attr 192
		bhvr 48
		elas 48
		accg 2
		fric 80
		setv ov02 0
		velo rand -15 15  rand -15 -1
		tick rand 100 300
		doif tmvt va00 va01 = 1
			mvto va00 va01
		else
			kill targ
		endi
	repe
	kill ownr
retn

subr moove
	doif ov10 = 0
		anim [13 14 255]
	elif ov10 > 0
		anim [0 1 255]
	elif ov10 < 0
		anim [6 7 255]
	endi
retn

subr front
	doif ov10 > 0
		anim [5]
		over
	elif ov10 < 0
		anim [11]
		over
	endi
	setv ov10 0
	gsub moove
retn
endm


*tickscript for egg

scrp 2 18 31932 9
doif pose = 0
	pose 1
	tick rand 30 100
elif pose = 1
	inst
	setv va00 posl
	setv va01 post
	subv va01 40
	setv va05 ov05
	doif va05 ne 0 and va05 ne 16 and va05 ne 32 and va05 ne 48
		setv va05 0
	endi
	snde "tpl2"
	inst
	new: simp 2 15 31932 "ngtripple" 64 0 501
		attr 199
		bhvr 59
		elas 30
		fric 95
		accg 3
		aero 2
		perm 60
		setv ov05 va05	
		setv ov00 60
		setv ov01 0
		setv ov02 50
		setv ov10 0
		base ov05
		anim [13 14 255]
		tick rand 10 50
		velo rand -5 5 rand -5 -1
		doif tmvt va00 va01 = 1
			mvto va00 va01
		else
			kill targ
		endi
	doif ov66 = 66
		kill ownr
	endi
	targ ownr
	doif ov66 ne 67
		pose 2
	else
		kill ownr
	endi
else
	kill ownr
endi

endm

*tickscript for old egg

scrp 2 18 31900 9
doif pose = 0
	pose 1
	tick rand 30 100
elif pose = 1
	inst
	setv va00 posl
	setv va01 post
	subv va01 40
	setv va05 ov05
	doif va05 ne 0 and va05 ne 16 and va05 ne 32 and va05 ne 48
		setv va05 0
	endi
	snde "tpl2"
	inst
	new: simp 2 15 31932 "ngtripple" 64 0 501
		attr 199
		bhvr 59
		elas 30
		fric 95
		accg 3
		aero 2
		perm 60
		setv ov05 va05	
		setv ov00 60
		setv ov01 0
		setv ov02 50
		setv ov10 0
		base ov05
		anim [13 14 255]
		tick rand 10 50
		velo rand -5 5 rand -5 -1
		doif tmvt va00 va01 = 1
			mvto va00 va01
		else
			kill targ
		endi
	doif ov66 = 66
		kill ownr
	endi
	targ ownr
	doif ov66 ne 67
		pose 2
	else
		kill ownr
	endi
else
	kill ownr
endi

endm

*eatscrript
scrp 2 15 31932 12
	lock
	snde "tpl1"
	stim writ from 80 4
	kill ownr 
endm



*eatscript for egg

scrp 2 18 31932 12
	lock
	snde "chwp"
	stim writ from 80 2
	kill ownr 
endm

*tripple push
scrp 2 15 31932 1
	snde "tpl0"
	velo rand -5 5 rand -5 -1
	stim writ from 86 1
endm

*tripple pull
scrp 2 15 31932 2
	snde "tpl0"
	velo rand -5 5 rand -5 -1
	stim writ from 86 1
endm
*tripple hit
scrp 2 15 31932 3
	snde "tpl0"
	velo rand -5 5 rand -5 -1
	stim writ from 87 1
endm
scrp 2 15 31932 4
	snde "tpl5"
	stim writ from 85 1
	tick 0
endm
scrp 2 15 31932 5
	velo rand -5 5 rand -5 -1
	tick 1
	setv ov98 0
	setv ov96 0
	accg 3
	aero 1
endm
scrp 2 15 31932 6

	doif wall = rght
		setv ov10 -1
		setv ov98 0
	elif wall = left
		setv ov10 1
		setv ov98 0
	endi
endm

*nest push, pullhit
scrp 2 17 31932 1
doif ov00 > 0
	subv ov00 1
	tick 1
else
	kill ownr
endi

endm 
*********
scrp 2 17 31932 2
doif ov00 > 0
	subv ov00 1
	tick 1
else
	kill ownr
endi

endm 
*********
scrp 2 17 31932 3
doif ov00 > 0
	subv ov00 1
	tick 1
else
	kill ownr
endi
targ from
doif targ <> null
	driv 0 -0.45
	driv 10 -0.35
	driv 12 -0.35
endi
targ ownr
endm 
*********
*remoove scripts
rscr

enum 2 15 31932 kill targ next
enum 2 17 31932 kill targ next
enum 2 18 31932 kill targ next
scrx 2 15 31932 1 scrx 2 15 31932 2 scrx 2 15 31932 3 scrx 2 15 31932 4 scrx 2 15 31932 5 scrx 2 15 31932 6 scrx 2 15 31932 9 scrx 2 15 31932 12 scrx 2 17 31932 1 scrx 2 17 31932 2 scrx 2 17 31932 3 scrx 2 18 31932 9 scrx 2 18 31932 12  scrx 2 15 31932 9 scrx 2 15 31932 12

endm
